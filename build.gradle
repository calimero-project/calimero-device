plugins {
  id "com.github.ben-manes.versions" version "0.49.0"
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'eclipse'
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

ext.junitJupiterVersion = '5.10.1'

group = 'io.calimero'
version = '3.0-SNAPSHOT'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(Jar).configureEach {
    from("${projectDir}") {
        include 'LICENSE'
        into "META-INF"
    }
    if (name == 'sourcesJar') {
        from("${projectDir}") {
            include 'README.md'
        }
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

compileJava.options*.compilerArgs = [
	'-Xlint:all,-serial'
]

compileTestJava.options*.compilerArgs = [
	'-Xlint:all',
	'-Xlint:-try',
]

tasks.named('compileJava') {
    options.javaModuleVersion = provider { project.version }
}

test {
	useJUnitPlatform() {
		excludeTags 'knxnetip'
	}
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption("Xdoclint:-missing", '-quiet')
}

dependencies {
	api "io.calimero:calimero-core:${version}"

	testImplementation "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
	testRuntimeOnly 'org.slf4j:slf4j-jdk-platform-logging:2.0.9'
	testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.9'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            pom {
                name = 'Calimero KNX Device'
                description = 'Communication stack for a Calimero KNX Device'
                url = 'https://github.com/calimero-project/calimero-device'
                inceptionYear = '2006'
                licenses {
                    license {
                        name = "GNU General Public License, version 2, with the Classpath Exception"
                        url = 'LICENSE'
                    }
                }
                developers {
                    developer {
                        name = "Boris Malinowsky"
                        email = "b.malinowsky@gmail.com"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/calimero-project/calimero-device.git'
                    url = 'https://github.com/calimero-project/calimero-device.git'
                }
            }
        }
    }
    repositories {
        maven {
            name 'maven'
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
        }
    }
}

signing {
    if (project.hasProperty("signing.keyId")) {
        sign publishing.publications.mavenJava
    }
}
